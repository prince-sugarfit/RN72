// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        compileSdkVersion = 33
        buildToolsVersion = "33.0.0"
        targetSDKVersion = 33
        minSDKVersion = 23
        kotlinVersion = "1.6.20"
        googlePlayServicesVersion = "18.0.0"
        googlePlayServicesVisionVersion = "20.1.2"
        authVersion = "18.0.0"
        supportLibVersion = "28.0.0"
        androidMapsUtilsVersion = "0.5+"
        firebaseIidVersion = "21.1.0"
        // https://github.com/StasDoskalenko/react-native-google-fit/issues/176
        fitnessVersion = "20.0.0"

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
        set('react-native', [
                versions: [
                        // Overriding Library SDK Versions
                        android : [
                                minSdk    : ext.minSDKVersion,
                                targetSdk : ext.targetSDKVersion,
                                compileSdk: ext.compileSdkVersion,
                                buildTools: ext.buildToolsVersion,
                        ],
                        firebase: [bom : "29.0.2"],
                ],
        ])
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
        classpath 'com.netflix.nebula:gradle-dependency-lock-plugin:4.9.2'
        classpath 'com.android.tools.build:gradle'
        classpath 'com.facebook.react:react-native-gradle-plugin'
    }
}

apply plugin: 'java'

subprojects { subproject ->
    afterEvaluate{
        if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
                defaultConfig {
                    minSdkVersion rootProject.ext.minSDKVersion
                    targetSdkVersion rootProject.ext.targetSDKVersion
                }
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

